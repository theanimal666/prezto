#
# Forked from paradox theme
#
# This theme requires a patched Powerline font, get them from
# https://github.com/Lokaltog/powerline-fonts.
#
# Authors:
#   Isaac Wolkerstorfer <i@agnoster.net>
#   Jeff Sandberg <meines460@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Christian Hubinger <christian@trigo.at>
#

# Load dependencies.
pmodload 'helper'

# Define variables.
_prompt_meines_current_bg='NONE'
_prompt_meines_segment_separator=''
_prompt_meines_start_time=$SECONDS

function prompt_meines_start_segment {
  local bg fg
  [[ -n "$1" ]] && bg="%K{$1}" || bg="%k"
  [[ -n "$2" ]] && fg="%F{$2}" || fg="%f"
  if [[ "$_prompt_meines_current_bg" != 'NONE' && "$1" != "$_prompt_meines_current_bg" ]]; then
    print -n " $bg%F{$_prompt_meines_current_bg}$_prompt_meines_segment_separator$fg "
  else
    print -n "$bg$fg "
  fi
  _prompt_meines_current_bg="$1"
  [[ -n "$3" ]] && print -n "$3"
}

function prompt_meines_end_segment {
  if [[ -n "$_prompt_meines_current_bg" ]]; then
    print -n " %k%F{$_prompt_meines_current_bg}$_prompt_meines_segment_separator"
  else
    print -n "%k"
  fi
  print -n "%f"
  _prompt_meines_current_bg=''
}

# ✘ ▃ ʿ««

function prompt_meines_build_prompt {
  prompt_meines_start_segment black default '%(?:%F{green}╭─:%F{red}╭─)%(!:%F{yellow}⚡ :)%(1j:%F{cyan}⚙ :)%(?:%F{green}:%F{red})\{%n@%m}─%f'
  prompt_meines_start_segment black white '$_prompt_meines_pwd'

  if [[ -n "$git_info" ]]; then
    prompt_meines_start_segment black green '${(e)git_info[ref]}${(e)git_info[status]}'
  fi

  prompt_meines_end_segment
}

function prompt_meines_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_meines_pwd="$MATCH"
    unset MATCH
  else
    _prompt_meines_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_meines_print_elapsed_time {
  local end_time=$(( SECONDS - _prompt_meines_start_time ))
  local hours minutes seconds remainder

  if (( end_time >= 3600 )); then
    hours=$(( end_time / 3600 ))
    remainder=$(( end_time % 3600 ))
    minutes=$(( remainder / 60 ))
    seconds=$(( remainder % 60 ))
    print -P "%B%F{red}>>> elapsed time ${hours}h${minutes}m${seconds}s%b"
  elif (( end_time >= 60 )); then
    minutes=$(( end_time / 60 ))
    seconds=$(( end_time % 60 ))
    print -P "%B%F{yellow}>>> elapsed time ${minutes}m${seconds}s%b"
  elif (( end_time > 10 )); then
    print -P "%B%F{green}>>> elapsed time ${end_time}s%b"
  fi
}

function prompt_meines_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_meines_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Calculate and print the elapsed time.
  prompt_meines_print_elapsed_time
}

function prompt_meines_preexec {
  _prompt_meines_start_time="$SECONDS"
}

function prompt_meines_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook preexec prompt_meines_preexec
  add-zsh-hook precmd prompt_meines_precmd

  # Set editor-info parameters.
  # zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%(?:%F{green}╰─▶:%F{red}╰─▶)%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%B%F{red}❮%f%}'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ' ⁝ %s'
  zstyle ':prezto:module:git:info:added' format ' %F{green}✚%F{reset}'
  zstyle ':prezto:module:git:info:ahead' format ' ↑'
  zstyle ':prezto:module:git:info:behind' format ' ↓'
  zstyle ':prezto:module:git:info:branch' format '%F{red}(%b)%F{reset}'  #' '
  zstyle ':prezto:module:git:info:commit' format '➦ %.7c'
  zstyle ':prezto:module:git:info:deleted' format ' %F{red}✖%F{reset}'
  zstyle ':prezto:module:git:info:dirty' format ' ⁝'
  zstyle ':prezto:module:git:info:clean' format ' %B%F{green}✓%F{reset}%b'
  zstyle ':prezto:module:git:info:modified' format ' %F{yellow}✱%F{reset}'
  zstyle ':prezto:module:git:info:position' format '%p'
  zstyle ':prezto:module:git:info:renamed' format ' ➙'
  zstyle ':prezto:module:git:info:stashed' format ' S'
  zstyle ':prezto:module:git:info:unmerged' format ' ═'
  zstyle ':prezto:module:git:info:untracked' format ' ?'
  zstyle ':prezto:module:git:info:keys' format \
    'ref' '$(coalesce "%b" "%p" "%c" )' \
    'status' '%s%D%A%B%S%C%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='
${(e)$(prompt_meines_build_prompt)}
 ${editor_info[keymap]} '
  RPROMPT='%F{blue}[%F{green}%D{%H:%M:%S}%F{blue}]%f'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_meines_setup "$@"
